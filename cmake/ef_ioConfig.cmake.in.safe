if (CMAKE_VERSION VERSION_LESS 2.8.9)
    message(FATAL_ERROR "ef_io requires at least CMake version 2.8.9")
endif()

if (NOT ef_io_FIND_COMPONENTS)
    set(ef_io_NOT_FOUND_MESSAGE "The ef_io package requires at least one component")
    set(ef_io_FOUND False)
    return()
endif()

set(_ef_io_FIND_PARTS_REQUIRED)
if (ef_io_FIND_REQUIRED)
    set(_ef_io_FIND_PARTS_REQUIRED REQUIRED)
endif()
set(_ef_io_FIND_PARTS_QUIET)
if (ef_io_FIND_QUIETLY)
    set(_ef_io_FIND_PARTS_QUIET QUIET)
endif()

get_filename_component(_ef_io_install_prefix "${CMAKE_CURRENT_LIST_DIR}" ABSOLUTE)

set(_ef_io_NOTFOUND_MESSAGE)

# Let components find each other, but don't overwrite CMAKE_PREFIX_PATH
set(_ef_io_CMAKE_PREFIX_PATH_old ${CMAKE_PREFIX_PATH})
set(CMAKE_PREFIX_PATH ${_ef_io_install_prefix})

foreach(module ${ef_io_FIND_COMPONENTS})
    find_package(ef_io${module}
        ${_ef_io_FIND_PARTS_QUIET}
        ${_ef_io_FIND_PARTS_REQUIRED}
        PATHS "${_ef_io_install_prefix}" NO_DEFAULT_PATH
    )
    if (NOT ef_io${module}_FOUND)
        if (ef_io_FIND_REQUIRED_${module})
            set(_ef_io_NOTFOUND_MESSAGE "${_ef_io_NOTFOUND_MESSAGE}Failed to find ef_io component \"${module}\" config file at \"${_ef_io_install_prefix}/ef_io${module}/ef_io${module}Config.cmake\"\n")
        elseif(NOT ef_io_FIND_QUIETLY)
            message(WARNING "Failed to find ef_io component \"${module}\" config file at \"${_ef_io_install_prefix}/ef_io${module}/ef_io${module}Config.cmake\"")
        endif()
    endif()

    # For backward compatibility set the LIBRARIES variable
    list(APPEND ef_io_LIBRARIES "ef_io::${module}")
endforeach()

# Restore the original CMAKE_PREFIX_PATH value
set(CMAKE_PREFIX_PATH ${_ef_io_CMAKE_PREFIX_PATH_old})

if (_ef_io_NOTFOUND_MESSAGE)
    set(ef_io_NOT_FOUND_MESSAGE "${_ef_io_NOTFOUND_MESSAGE}")
    set(ef_io_FOUND False)
endif()

